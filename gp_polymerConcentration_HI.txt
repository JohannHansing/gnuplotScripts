## first run: plot_D_over_p_vary_aop_constU.sh 0 ewaldCorr/steric 10 0.5 1 2 ##
## AND !!! plot_D_over_p_vary_sterictype_at_U0_epstex.sh 0 LJ ##

set autoscale; unset log; unset label; set ytic auto; set xtic auto; set key default
set key top right horiz width -6
set ylabel "$D/D_0$" offset 1.5
set xlabel "$XLABEL TODO$"
set xrange [-21:21]
set yrange [-0.01:1.3]


# TICS
set mxtics             # activate minor xtics
set mytics             # activate minor ytics
set xtics scale 1.1, 0.6 
set ytics scale 1.1, 0.6


set border linewidth 1.5

set style data linespoints
set pointintervalbox 1

set style line 1 lw 2 pi -1 ps 1.5
set tmargin at screen 0.98
set terminal epslatex color size 5.4, 3.1 font " " 12 lw 1.5
set output '/Users/jh/Documents/workspace-cpp/gnuplot/tmp/tmp_output.tex'
set xlabel '$C_\mathrm{polymer}  [a.u.]$'
#set xlabel 'Polymer concentration [a.u.]'
set autoscale
set yrange [0:1.2]
set xrange [0:0.55]
set key top right horiz width 1

# FUNCTION 
m_a=162*1.66
a_ex=0.39
p_ex=1.9
#~~~~~~~~~ I NEED TO INCLUDE aop TERM FOR HI COMPARISON ~~~~~~~~~~
#C2(p_s)=(p_s**3 *m_a / (a_ex*(p_ex+a_ex)**3)) * (3 * (p_ex+a_ex)/p_s - 2 * a_ex)
C2HI(p_rel)=m_a*(p_rel/p_ex)**3  *  ( 3 * p_ex / (p_rel * a_ex) - 2  )

# FTTING
f1(x)=exp( - a1 * x ** n1 )
a1 = 5.559; n1 = 0.6;            # initial guess for a1 and b1
f2(x)=exp( - a2 * x ** n2 ); a2 = 6; n2 = 0.6;
f3(x)=exp( - a3 * x ** n3 ); a3 = 6; n3 = 0.6; 
set fit errorvariables
fit f1(x) "tmp/plotdata_3.txt" u (($1*0.5)**2/1.2*1.66):($2/6):($3/6) via a1,n1
# This is the correction to not overcount edgeparticle at origin of cell
# fit f1(x) "tmp/plotdata_3.txt" u TODO correct this calculation first: ((($1*0.5)**2-0.2/3*$1**3)/1.2*1.66):($2/6):($3/6) via a1,n1
fit f2(x) "tmp/plotdata_4.txt" u ($1**2/1.2*1.66):($2/6):($3/6) via a2,n2
fit f3(x) "tmp/plotdata_5.txt" u (($1*2)**2/1.2*1.66):($2/6):($3/6) via a3,n3
set print "tmp/fitparameters.txt"
print "a1 ", a1, a1_err
print "n1 ", n1, n1_err
print "a2 ", a2, a2_err
print "n2 ", n2, n2_err
print "a3 ", a3, a3_err
print "n3 ", n3, n3_err

#  PLOTTING 
# Note that $1 corresponds to the p/b value. We transform it to the a/b value.
# by use of a/b = p/b * a/p
plot \
"tmp/plotdata_1.txt" u (($1*1)**2/1.2*1.66):($2/6):($3/6) w points ls 1 pt 1 lc rgb 'black' ti "no HI", -1.0/1.4 * x + 1 dashtype 2 lc rgb '#A9A9A9' lw 3 ti "", \
"tmp/plotdata_3.txt" u (($1*0.5)**2/1.2*1.66):($2/6):($3/6) w points ls 1 pt 5 lc rgb '#FF0000' ti "$a/p=0.5$" , \
"tmp/plotdata_4.txt" u (($1*1)**2/1.2*1.66):($2/6):($3/6) w points ls 1 pt 5 lc rgb '#0000FF' ti "$a/p=1$", \
"tmp/plotdata_5.txt" u (($1*2)**2/1.2*1.66):($2/6):($3/6) w points ls 1 pt 5 lc rgb '#32CD32' ti "$a/p=2$", \
f1(x) lc rgb '#FF0000' notitle, f2(x) lc rgb '#0000FF' notitle,  f3(x) lc rgb '#32CD32' notitle 

set output "/Users/jh/Documents/workspace-cpp/gnuplot/figures/epslatex/D_over_Cpoly_stericOnly_at_U0_d0_with_newHI.tex"
replot
set term aqua
replot